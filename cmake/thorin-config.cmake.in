# Provides Thorin library and include path.
# Once done this will define
#
# Thorin_INCLUDE_DIRS (including dependencies to LLVM/RV/Half)
# Thorin_LIBRARIES (including dependencies to LLVM/RV)
# Thorin_FOUND
# Thorin_HAS_LLVM_SUPPORT | Thorin_HAS_RV_SUPPORT

cmake_minimum_required(VERSION 3.1)

find_path(Thorin_DIR
    NAMES thorin-config.cmake
    PATHS
        ${Thorin_DIR} ${AnyDSL_thorin_DIR} ${THORIN_DIR}
        $ENV{Thorin_DIR} $ENV{THORIN_DIR}
        ${CMAKE_CURRENT_LIST_DIR}
        "@CMAKE_CURRENT_BINARY_DIR@"
        "@CMAKE_INSTALL_PREFIX@"
    PATH_SUFFIXES
        share/thorin/cmake
)

find_path(THORIN_ROOT_DIR
    NAMES cmake/thorin-config.cmake.in src/thorin/world.h
    PATHS ${Thorin_DIR} $ENV{Thorin_DIR} ${THORIN_ROOT_DIR} "@CMAKE_CURRENT_SOURCE_DIR@" "@CMAKE_SOURCE_DIR@")
list(APPEND CMAKE_MODULE_PATH "${THORIN_ROOT_DIR}/cmake/modules")

find_path(Half_DIR NAMES half.hpp PATHS ${Half_DIR} $ENV{Half_DIR} "@Half_DIR@" "@Half_INCLUDE_DIR@")
find_package(Half REQUIRED)

set(Thorin_HAS_LLVM_SUPPORT @LLVM_FOUND@)
set(Thorin_HAS_RV_SUPPORT @RV_FOUND@)

if(Thorin_HAS_LLVM_SUPPORT)
    find_path(LLVM_DIR LLVMConfig.cmake PATHS ${LLVM_DIR} $ENV{LLVM_DIR} "@LLVM_DIR@")
    find_package(LLVM CONFIG QUIET)

    if(Thorin_HAS_RV_SUPPORT)
        find_path(RV_DIR rv-config.cmake PATHS ${RV_DIR} $ENV{RV_DIR} "@RV_DIR@")
        find_package(RV QUIET)
    endif()
endif()

find_path(THORIN_INCLUDE_DIR NAMES thorin/world.h PATHS ${THORIN_ROOT_DIR}/src)

if(NOT TARGET thorin)
    include(${Thorin_DIR}/thorin-exports.cmake)
endif()
set(THORIN_LIBRARY thorin)

set(THORIN_DEPS_INCLUDE_DIRS
    ${Half_INCLUDE_DIRS}
    ${LLVM_INCLUDE_DIRS}
    ${RV_INCLUDE_DIR}
)

set(Thorin_LIBRARIES ${THORIN_LIBRARY})
set(Thorin_INCLUDE_DIRS ${THORIN_INCLUDE_DIR} ${THORIN_DEPS_INCLUDE_DIRS})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(Thorin DEFAULT_MSG Thorin_DIR THORIN_INCLUDE_DIR)

mark_as_advanced(THORIN_LIBRARY THORIN_INCLUDE_DIR THORIN_ROOT_DIR)
