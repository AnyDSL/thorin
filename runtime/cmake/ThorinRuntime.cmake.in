CMAKE_MINIMUM_REQUIRED ( VERSION 3.0 )

INCLUDE ( CMakeParseArguments )

LIST ( APPEND THORIN_RUNTIME_LIBRARIES "@CONF_RUNTIME_LIBRARIES@" )

# Find impala
FIND_PROGRAM ( IMPALA_BIN impala )
IF ( NOT IMPALA_BIN )
    MESSAGE ( FATAL_ERROR "Could not find impala binary, it has to be in the PATH" )
ENDIF()

FIND_PROGRAM ( LLVM_AS_BIN llvm-as
    PATHS
        ${LLVM_TOOLS_BINARY_DIR}
        ${LLVM_INSTALL_PREFIX}/bin
    PATH_SUFFIXES
        ${CMAKE_CONFIGURATION_TYPES}
)
FIND_PROGRAM ( CLANG_BIN clang
    PATHS
        ${LLVM_TOOLS_BINARY_DIR}
        ${LLVM_INSTALL_PREFIX}/bin
    PATH_SUFFIXES
        ${CMAKE_CONFIGURATION_TYPES}
)
IF ( NOT LLVM_AS_BIN )
    MESSAGE ( FATAL_ERROR "Could not find llvm-as binary, it has to be in the PATH" )
ENDIF()
IF ( NOT CLANG_BIN )
    MESSAGE ( FATAL_ERROR "Could not find clang binary, it has to be in the PATH" )
ENDIF()

# Find python for post-patcher.py
FIND_PACKAGE ( PythonInterp REQUIRED )
MESSAGE ( STATUS "Python found: ${PYTHON_VERSION_STRING}" )
SET ( PYTHON_BIN ${PYTHON_EXECUTABLE} )

SET ( BACKEND ${BACKEND} CACHE STRING "select the backend from the following: CPU, AVX, NVVM, CUDA, OPENCL, SPIR" )
IF ( NOT BACKEND )
    SET ( BACKEND cpu CACHE STRING "select the backend from the following: CPU, AVX, NVVM, CUDA, OPENCL, SPIR" FORCE )
ENDIF()

STRING ( TOLOWER "${BACKEND}" BACKEND )
MESSAGE ( STATUS "Selected backend: ${BACKEND}" )

SET ( OPT_FLAGS_DEBUG "-g" )
SET ( OPT_FLAGS_RELEASE "-O3" )

MACRO ( THORIN_RUNTIME_WRAP outfiles )
    CMAKE_PARSE_ARGUMENTS("TRW" "" "BACKEND" "CLANG_FLAGS;CLANG_FLAGS_RELEASE;CLANG_FLAGS_DEBUG;IMPALA_FLAGS;IMPALA_FLAGS_RELEASE;IMPALA_FLAGS_DEBUG;FILES" ${ARGN})
    IF ( NOT "${TRW_UNPARSED_ARGUMENTS}" STREQUAL "" )
        MESSAGE ( FATAL_ERROR "Unparsed arguments ${TRW_UNPARSED_ARGUMENTS}" )
    ENDIF ()

    # Get last filename, and absolute filenames
    SET ( _infiles )
    FOREACH ( _it ${TRW_FILES} )
        GET_FILENAME_COMPONENT ( _infile ${_it} ABSOLUTE )
        SET ( _infiles ${_infiles} ${_infile} )
        SET ( _lastfile ${_it} )
    ENDFOREACH ()

    # Add all input files as one impala job
    GET_FILENAME_COMPONENT ( _basename ${_lastfile} NAME_WE )
    SET ( _binary_basepath ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_CFG_INTDIR} )
    SET ( _llfile ${_binary_basepath}/${_basename}.ll )
    SET ( _objfile ${_binary_basepath}/${_basename}.o )

    SET ( _impala_platform
        ${THORIN_RUNTIME_DIR}/runtime.impala
        ${THORIN_RUNTIME_DIR}/platforms/intrinsics_thorin.impala
        ${THORIN_RUNTIME_DIR}/platforms/intrinsics_${TRW_BACKEND}.impala )

    SET ( _impala_flags_RELEASE ${OPT_FLAGS_RELEASE} ${TRW_IMPALA_FLAGS_RELEASE})
    SET ( _impala_flags_DEBUG ${OPT_FLAGS_DEBUG} ${TRW_IMPALA_FLAGS_DEBUG})
    SET ( _impala_flags -emit-llvm -o ${_binary_basepath}/${_basename} ${TRW_IMPALA_FLAGS} $<$<CONFIG:Release>:${_impala_flags_RELEASE}>$<$<CONFIG:Debug>:${_impala_flags_DEBUG}> )

    SET ( _clang_flags_RELEASE ${OPT_FLAGS_RELEASE} ${TRW_CLANG_FLAGS_RELEASE})
    SET ( _clang_flags_DEBUG ${OPT_FLAGS_DEBUG} ${TRW_CLANG_FLAGS_DEBUG})
    SET ( _clang_flags ${TRW_CLANG_FLAGS} $<$<CONFIG:Release>:${_clang_flags_RELEASE}>$<$<CONFIG:Debug>:${_clang_flags_DEBUG}> )

    # Generate .ll file and patch it
    ADD_CUSTOM_COMMAND ( OUTPUT ${_llfile}
        COMMAND ${IMPALA_BIN} ${_impala_flags} ${_infiles} ${_impala_platform}
        COMMAND ${PYTHON_BIN} ${THORIN_RUNTIME_DIR}/post-patcher.py ${TRW_BACKEND} ${_binary_basepath}/${_basename}
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        DEPENDS ${IMPALA_BIN} ${THORIN_LIBRARY} ${PYTHON_BIN} ${THORIN_RUNTIME_DIR}/post-patcher.py ${_impala_platform} ${_infiles} VERBATIM )
    
    # Generate .bc files for NVVM & SPIR
    IF ( "${TRW_BACKEND}" STREQUAL "spir" OR "${TRW_BACKEND}" STREQUAL "nvvm" )
        SET ( _irfile ${CMAKE_CURRENT_BINARY_DIR}/${_basename}.${TRW_BACKEND} )
        SET ( _bcfile ${CMAKE_CURRENT_BINARY_DIR}/${_basename}.${TRW_BACKEND}.bc )
        ADD_CUSTOM_COMMAND ( OUTPUT ${_bcfile} COMMAND ${LLVM_AS_BIN} ${_irfile} DEPENDS ${_irfile} VERBATIM )
        SET_SOURCE_FILES_PROPERTIES ( ${_irfile} $_bcfile} PROPERTIES GENERATED true )
    ENDIF()

    # Run clang on the patched .ll file to get the .o
    ADD_CUSTOM_COMMAND ( OUTPUT ${_objfile}
        COMMAND ${CLANG_BIN} ${_clang_flags} -c -o ${_objfile} ${_llfile}
        DEPENDS ${_llfile} VERBATIM )

    SET_SOURCE_FILES_PROPERTIES ( ${_objfile} PROPERTIES EXTERNAL_OBJECT true GENERATED true LANGUAGE CXX )
    SET ( ${outfiles} ${${outfiles}} ${_objfile} ${_bcfile} ${_irfile} )
ENDMACRO ()
